{价格高低差}
ST01:=HIGH - LOW;
ST:=ST01;
{平台}
PT:=REF(ST,1);
重心:(HIGH + LOW + CLOSE)/3,COLORRED, NODRAW;

{确定周期, 当日9点前取前一天的数据, 给当天操作提前下单做准备}
XS:=FROMOPEN;
CYCLE:=1;

{换手率: TURNOVER RATE}
R01:=VOL/CAPITAL;
RATE:=1 + R01;

{两条压力线}
YL01:=2*重心-LOW;
YL02:=重心+PT;
YL1:=IFF(YL01<YL02,YL01,YL02);
YL2:=IFN(YL01<YL02,YL01,YL02);
压2:REF(YL2, CYCLE),NODRAW;
压1:REF(YL1, CYCLE),NODRAW;

{两条支撑线}
ZC01:=2*重心-HIGH;
ZC02:=重心-PT;
ZC1:=IFF(ZC01>ZC02,ZC01,ZC02);
ZC2:=IFN(ZC01>ZC02,ZC01,ZC02);
支1:REF(ZC1, CYCLE),NODRAW;
支2:REF(ZC2, CYCLE),NODRAW;

{预判上涨概率}
CHOICE:=777;
P0:=1.000;
P1:=(YL1-CLOSE)*100;
P2:=(CLOSE-ZC1)*100;
P3:=IFF(P2>=0.02, 1-P2/(P1+P2),IFF(P1<0.02, 0.500,P0));
P4:=REF(P3,CYCLE) * 100;
PX:=IFF(P4>49 AND P4<51, CHOICE, P4);
上涨概率:PX, NODRAW;
ICON:=IFF(REF(CLOSE,CYCLE)>REF(REF(CLOSE,CYCLE+1),1),38,39);
DRAWICON(PX= CHOICE, CLOSE*0.9,ICON);

{止盈止损}
ZLW0:=IF((CLOSE > 200),(CLOSE * 1.01),(CLOSE * 1.07));
ZLW1:=IF((CLOSE < 10),(CLOSE * 1.05),ZLW0);
ZSW0:=IF((CLOSE > 200),(CLOSE * 0.99),(CLOSE * 0.93));
ZSW1:=IF((CLOSE < 10),(CLOSE * 0.95),ZSW0);

止损:ZSW1,COLORRED,NODRAW;
止盈:ZLW1, COLORGREEN,NODRAW;

{布林线}
SD:=20;
WIDTH:=2;
DIS:=STDP(CLOSE,SD);
MID:MA(CLOSE,SD),COLORFFAEC9;
UPPER:MID+WIDTH*DIS,COLORFFC90E;
LOWER:MID-WIDTH*DIS,COLOR0CAEE6;
新:CLOSE,COLORWHITE;